def winningTeamNextItems($n):
  . as $in
  | length as $length
  | .[$n] as $nth
  | $nth[:5] as $currentItems
  | [foreach range($n+1; $length) as $nextStateIndex
    ([[],[],[],[],[]];
      [range(0;5) as $playerIndex |
      if .[$playerIndex] == [] then
        ($in[$nextStateIndex][$playerIndex]-$currentItems[$playerIndex]) as $diffItems |
        if $diffItems == [] then
          []
        else
          $diffItems[0]
        end
      else
        .[$playerIndex]
      end]
    ;
    if $nextStateIndex >= $length-1 then
      if . == [[],[],[],[],[]] then
        empty
      else
        .
      end
    elif map(. == []) | any then
      empty
    else
      .
    end
    )] | if . == [] then empty else first end
;

[.[] | { "gameId": .gameId,
        "winningTeamNextItems": (.itemsTimeline | [ range(0;length) as $i
                                    | winningTeamNextItems($i) ])
}]
